$IFDEF UNIDATA
$BASICTYPE "P"
$ENDIF
       SUBROUTINE u2_getBooksForAuthor( SearchData, BookList, ErrText)
* ---------------------------------------------------------------------------
*  @@Name        : u2_getBooksForAuthor
*  @@Description : Get an array of summary details for all books by an author
*  @@Version     : 1.0
* ---------------------------------------------------------------------------
*  Brief Description
*  -----------------
*  @@INFO
* ---------------------------------------------------------------------------
*  Warnings
*  --------
*
* ---------------------------------------------------------------------------
*  Modification History
*  --------------------
*  @@Log
* ---------------------------------------------------------------------------
* Keywords
* --------
*
* --------------------------------------------------------------------------
* To Do List
* ----------
*
* --------------------------------------------------------------------------
$IFDEF UNIVERSE
$OPTIONS PICK
$ENDIF
$INCLUDE u2_common.h

$INCLUDE books.inc U2_AUTHORS.h
$INCLUDE books.inc U2_BOOKS.h
$INCLUDE books.inc U2_BOOK_SUMMARY.h

       BookList = ''
       ErrText = ''

       AuthorId = SearchData<1>
       Intl     = SearchData<2>

       Read AuthorRec From F.AUTHORS, AuthorId Else
          AuthorRec = ''
       End
       NumberOfBooks = DCOunt(AuthorRec<U2_AUTHORS.BOOK_IDS>,@VM)
       If Not(NumberOfBooks) Then
          ErrText = 'No books found for author ':AuthorId
          RETURN
       End

       If Intl Then
          PriceConv = "MD2,[,'.',',',]"
       End Else
          PriceConv = "MD2"
       End
       
       For BookNumber = 1 To NumberOfBooks
          BookId = AuthorRec<U2_AUTHORS.BOOK_IDS, BookNumber>
          GoSub AddBook
       Next BookNumber
       RETURN

*---------------------------------------------------------------------------
* AddBook - add a book to the index
*---------------------------------------------------------------------------
AddBook: 
       Read BookRec From F.BOOKS, BookId Else
          BookRec = ''
          BookRec<U2_BOOKS.TITLE> = 'Not Found'
       End
       BookList<U2_BOOK.SUMMARY_TITLE_ID, BookNumber> = BookId
       BookList<U2_BOOK.SUMMARY_TITLE, BookNumber> = BookRec<U2_BOOKS.TITLE>
       BookList<U2_BOOK.SUMMARY_AUTHOR_NAME, BookNumber> = AuthorRec<U2_AUTHORS.FULLNAME>
       BookList<U2_BOOK.SUMMARY_ISBN, BookNumber> = BookRec<U2_BOOKS.ISBN>

       BookList<U2_BOOK.SUMMARY_SALE_PRICE, BookNumber> = OConv(BookRec<U2_BOOKS.SALE_PRICE>,PriceConv)
       BookList<U2_BOOK.SUMMARY_MEDIA, BookNumber> = OConv(Field(BookRec<U2_BOOKS.MEDIA>," ",1,1),"MCU")

       Return


       Return




