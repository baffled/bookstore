$IFDEF UNIDATA
$BASICTYPE "P"
$ENDIF
       SUBROUTINE u2_getBooks( SearchData, BookList, ErrText)
* ---------------------------------------------------------------------------
*  @@Name        : u2_getBooks
*  @@Description : Get an array of summary details for keyword search
*  @@Version     : 1.0
* ---------------------------------------------------------------------------
*  Brief Description
*  -----------------
*  @@INFO
* ---------------------------------------------------------------------------
*  Warnings
*  --------
*
* ---------------------------------------------------------------------------
*  Modification History
*  --------------------
*  @@Log
* ---------------------------------------------------------------------------
* Keywords
* --------
*
* --------------------------------------------------------------------------
* To Do List
* ----------
*
* --------------------------------------------------------------------------
$IFDEF UNIVERSE
$OPTIONS PICK
$ENDIF
$INCLUDE u2_common.h

$INCLUDE books.inc U2_AUTHORS.h
$INCLUDE books.inc U2_BOOKS.h
$INCLUDE books.inc U2_PUBLISHERS.h
$INCLUDE books.inc U2_BOOK_SUMMARY.h

       BookList = ''
       ErrText = ''
       SearchText = OConv(SearchData<1>,"MCU")
       Intl = SearchData<2>

       Query = \SELECT U2_BOOKS\
       Query := ' WITH ISBN = "': SearchText:'"'
       Query := ' OR WITH U_TITLE LIKE "...': SearchText:'..."'
       Query := ' OR WITH U_AUTHOR_NAME LIKE "...': SearchText:'..."'


       If Intl Then
          PriceConv = "MD2,[,'.',',',]"
       End Else
          PriceConv = "MD2"
       End

       BookNumber = 0
       Perform Query
       Done = @False
       Loop
          ReadNext BookId Else Done = @True
       Until Done Do
          BookNumber += 1
          GoSub AddBook
       Repeat

       RETURN

*---------------------------------------------------------------------------
* AddBook - add a book to the index
*---------------------------------------------------------------------------
AddBook:
       Read BookRec From F.BOOKS, BookId Else
          BookRec = ''
          BookRec<U2_BOOKS.TITLE> = 'Not Found'
       End
       BookList<U2_BOOK.SUMMARY_TITLE_ID, BookNumber> = BookId
       BookList<U2_BOOK.SUMMARY_TITLE, BookNumber> = BookRec<U2_BOOKS.TITLE>
       ReadV AuthorName From F.AUTHORS, BookRec<U2_BOOKS.AUTHOR_ID>, U2_AUTHORS.FULLNAME Else
          AuthorName = 'Not Found'
       End
       BookList<U2_BOOK.SUMMARY_AUTHOR_NAME, BookNumber> = AuthorName
       BookList<U2_BOOK.SUMMARY_ISBN, BookNumber> = BookRec<U2_BOOKS.ISBN>
       BookList<U2_BOOK.SUMMARY_SALE_PRICE, BookNumber> = OConv(BookRec<U2_BOOKS.SALE_PRICE>,PriceConv)
       BookList<U2_BOOK.SUMMARY_MEDIA, BookNumber> = OConv(Field(BookRec<U2_BOOKS.MEDIA>," ",1,1),"MCU")

       Return




