$BASICTYPE "P"
SUBROUTINE u2_getBooks( SearchData, BookList, ErrText)
* ---------------------------------------------------------------------------
*  @@Name        : u2_getBooks
*  @@Description : Get an array of summary details for keyword search
*  @@Version     : 1.0
* ---------------------------------------------------------------------------
*  Brief Description
*  -----------------
*  @@INFO
* ---------------------------------------------------------------------------
*  Warnings
*  --------
*
* ---------------------------------------------------------------------------
*  Modification History
*  --------------------
*  @@Log
* ---------------------------------------------------------------------------
* Keywords
* --------
*
* --------------------------------------------------------------------------
* To Do List
* ----------
*
* --------------------------------------------------------------------------
$INCLUDE u2_common.h
$INCLUDE books.inc U2_AUTHORS.h
$INCLUDE books.inc U2_BOOKS.h
$INCLUDE books.inc U2_PUBLISHERS.h
$INCLUDE books.inc U2_BOOK_SUMMARY.h
BookList = ''
ErrText = ''
SearchText = OCONV(SearchData<1>,"MCU")
Intl = SearchData<2>
Query = \SELECT U2_BOOKS\
Query := ' WITH ISBN = "': SearchText:'"'
Query := ' OR WITH U_TITLE LIKE "...': SearchText:'..."'
Query := ' OR WITH U_AUTHOR_NAME LIKE "...': SearchText:'..."'
IF Intl THEN
PriceConv = "MD2,[,'.',',',]"
END ELSE
PriceConv = "MD2"
END
BookNumber = 0
PERFORM Query
Done = @FALSE
LOOP
READNEXT BookId ELSE Done = @TRUE
UNTIL Done DO
BookNumber += 1
GOSUB AddBook
REPEAT
RETURN
*---------------------------------------------------------------------------
* AddBook - add a book to the index
*---------------------------------------------------------------------------
AddBook:
READ BookRec FROM F.BOOKS, BookId ELSE
BookRec = ''
BookRec<U2.BOOKS.TITLE> = 'Not Found'
END
BookList<U2.BOOK.SUMMARY.TITLE.ID, BookNumber> = BookId
BookList<U2.BOOK.SUMMARY.TITLE, BookNumber> = BookRec<U2.BOOKS.TITLE>
READV AuthorName FROM F.AUTHORS, BookRec<U2.BOOKS.AUTHOR.ID>, U2.AUTHORS.FULLNAME ELSE
AuthorName = 'Not Found'
END
BookList<U2.BOOK.SUMMARY.AUTHOR.NAME, BookNumber> = AuthorName
BookList<U2.BOOK.SUMMARY.ISBN, BookNumber> = BookRec<U2.BOOKS.ISBN>
BookList<U2.BOOK.SUMMARY.SALE.PRICE, BookNumber> = OCONV(BookRec<U2.BOOKS.SALE.PRICE>,PriceConv)
BookList<U2.BOOK.SUMMARY.MEDIA, BookNumber> = OCONV(FIELD(BookRec<U2.BOOKS.MEDIA>," ",1,1),"MCU")
RETURN
RETURN

