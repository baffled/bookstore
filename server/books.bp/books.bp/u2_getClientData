SUBROUTINE u2_getClientData( ClientId, ClientData, ErrText)
* ---------------------------------------------------------------------------
*  @@Name        : u2_getClientData
*  @@Description : Get a full set of data for a client
*  @@Version     : 1.0
* ---------------------------------------------------------------------------
*  Brief Description
*  -----------------
*  @@INFO
* ---------------------------------------------------------------------------
*  Warnings
*  --------
*
* ---------------------------------------------------------------------------
*  Modification History
*  --------------------
*  @@Log
* ---------------------------------------------------------------------------
* Keywords
* --------
*
* --------------------------------------------------------------------------
* To Do List
* ----------
*
* --------------------------------------------------------------------------
$OPTIONS PICK
$INCLUDE u2_common.h
$INCLUDE books.inc U2_CLIENTS.h
$INCLUDE books.inc U2_CLIENTDATA.h
$INCLUDE books.inc U2_CLIENT_ORDERS.h
$INCLUDE books.inc U2_ORDERS.h
$INCLUDE books.inc U2_PAYMENTS.h
ClientData= ''
ClientData<U2_CLIENTDATA.CLIENTID> = ClientId
Read ClientRec From F.CLIENTS, ClientId Else
ClientRec = ''
ErrText = 'Unknown Client'
RETURN
End
ClientData<U2_CLIENTDATA.SURNAME> = ClientRec<U2_CLIENTS.SURNAME>
ClientData<U2_CLIENTDATA.FORENAME> = ClientRec<U2_CLIENTS.FORENAME>
ClientData<U2_CLIENTDATA.ADDRESS> = ClientRec<U2_CLIENTS.ADDRESS>
ClientData<U2_CLIENTDATA.JOINDATE> = OConv(ClientRec<U2_CLIENTS.JOIN_DATE>,"D4/")
* Get client orders
Read CliOrderRec From F.CLIENT.ORDERS, ClientId Else
CliOrderRec = ''
End
NumOrders = DCount(CliOrderRec<U2_CLIENT.ORDERS_ORDER_IDS>,@VM)
For OrderNo = 1 To NumOrders
OrderId = CliOrderRec<U2_CLIENT.ORDERS_ORDER_IDS,OrderNo>
GoSub AddOrder
Next OrderNo
* Get payment history
Call u2_getClientPayments(ClientId, PaymentIds, ErrText)
NumPayments = DCount(PaymentIds,@FM)
For PayNo =1 To NumPayments
PayId = PaymentIds<PayNo>
GoSub AddPayment
Next PayNo
RETURN
* --------------------------------------------------------------------------
* Add Order
* --------------------------------------------------------------------------
AddOrder:
Read OrderRec From F.ORDERS, OrderId Else
Return
End
ClientData<U2_CLIENTDATA.ORDERIDS, OrderNo> = OrderId
ClientData<U2_CLIENTDATA.ORDERSTATUS, OrderNo> = OrderRec<U2_ORDERS.ORDER_STATUS>
ClientData<U2_CLIENTDATA.ORDERDATE, OrderNo> = OConv(Field(OrderId,"*",1),"D4/")
Total = 0                           
Call u2_calcOrderTotal(Total, OrderRec)
ClientData<U2_CLIENTDATA.ORDERTOTAL, OrderNo> = Total
Return
* --------------------------------------------------------------------------
* add payment
* --------------------------------------------------------------------------
AddPayment:
Read PaymentRec From F.PAYMENTS, PayId Else
Return
End
ClientData<U2_CLIENTDATA.PAYMENTIDS, PayNo> = PayId
ClientData<U2_CLIENTDATA.PAYMENTAMT, PayNo> = Int(PaymentRec<U2_PAYMENTS.AMOUNT>)
ClientData<U2_CLIENTDATA.PAYMENTDATE, PayNo> = OConv(PaymentRec<U2_PAYMENTS.PAY_DATE>,"D4/")
Return

